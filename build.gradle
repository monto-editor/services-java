buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    classpath 'gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.2'
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'com.github.johnrengelman.shadow'
  apply plugin: 'com.github.sherter.google-java-format'

  group = 'monto'

  sourceCompatibility = 1.8

  repositories {
    mavenCentral()
  }
}

project(':shared') {
  dependencies {
    // JavaDebugger and monto.service.java8.launching have JDI dependencies, that are not included in the standard Java API, but the tools.jar 
    compile files("${System.properties['java.home']}/../lib/tools.jar")
    compile project(':services-base-java')
    compile 'org.antlr:antlr4-runtime:4.5.3'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'com.github.javaparser:javaparser-core:2.3.0'
  }
}

project(':main') {
  jar {
    manifest {
      attributes('Main-Class': 'monto.service.java8.Main')
    }
  }

  dependencies {
    compile project(':shared')
  }
}

project(':benchmarks') {
  jar {
    manifest {
      attributes('Main-Class': 'monto.service.benchmark.IntegrationBenchmark')
    }
  }

  dependencies {
    compile project(':shared')
  }
}
